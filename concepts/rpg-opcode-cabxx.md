---
title: CABxx (Compare and Branch)
section: 777
---

# CABxx (Compare and Branch)

Free-Form Syntax: (not allowed - use other operation codes, such as LEAVE, ITER, and RETURN)

## Code

| Factor 1 | Factor 2 | Result Field | Indicators |
|----------|----------|--------------|------------|
| CABxx | Comparand | Comparand | Label | HI | LO | EQ |

The CABxx operation compares factor 1 with factor 2. If the condition specified by xx is true, the program branches to the TAG or ENDSR operation associated with the label specified in the result field. Otherwise, the program continues with the next operation in the sequence. If the result field is not specified, the resulting indicators (positions 71-76) are set accordingly, and the program continues with the next operation in the sequence.

You can specify conditioning indicators. Factor 1 and factor 2 must contain a literal, a named constant, a figurative constant, a table name, an array element, a data structure name, or a field name. Factor 1 and factor 2 must be of the same type. The label specified in the result field must be associated with a unique TAG operation and must be a unique symbolic name.

A CABxx operation in the cycle-main procedure can specify a branch:

- To a previous or a succeeding specification line
- From a detail calculation line to another detail calculation line
- From a total calculation line to another total calculation line
- From a detail calculation line to a total calculation line
- From a subroutine to a detail calculation line or a total calculation line.

A CABxx operation in a subprocedure can specify a branch:

- From a line in the body of the subprocedure to another line in the body of the subprocedure
- From a line in a subroutine to another line in the same subroutine
- From a line in a subroutine to a line in the body of the subprocedure

The CABxx operation cannot specify a branch from outside a subroutine to a TAG or ENDSR operation within that subroutine.

**Attention!** Branching from one point in the logic to another may result in an endless loop. You must ensure that the logic of your program or procedure does not produce undesirable results.

Resulting indicators are optional. When specified, they are set to reflect the results of the compare operation. For example, the HI indicator is set when F1>F2, LO is set when F1<F2, and EQ is set when F1=F2.

See "Compare Operations" on page 607 for the rules for comparing factor 1 with factor 2.

For more information, see "Branching Operations" on page 602.

## Example

```
 *..1....+....2....+....3....+....4....+....5....+....6....+....7...+....
 CL0N01Factor1+++++++Opcode(E)+Factor2+++++++Result++++++++Len++D+HiLoEq....
  *
  *         The field values are:
  *         FieldA = 100.00
  *         FieldB = 105.00
  *         FieldC = ABC
  *         FieldD = ABCDE
  *
  *          Branch to TAGX.
 C     FieldA        CABLT     FieldB        TAGX
  *
  *          Branch to TAGX.
 C     FieldA        CABLE     FieldB        TAGX
  *
  *          Branch to TAGX; indicator 16 is off.
 C     FieldA        CABLE     FieldB        TAGX                     16
  *
  *          Branch to TAGX; indicator 17 is off, indicator 18 is on.
 C     FieldA        CAB       FieldB        TAGX                 1718
  *
  *          Branch to TAGX; indicator 19 is on.
 C     FieldA        CAB       FieldA        TAGX                      19
  *
  *          No branch occurs.
 C     FieldA        CABEQ     FieldB        TAGX
  *
  *          No branch occurs; indicator 20 is on.
 C     FieldA        CABEQ     FieldB        TAGX                   20
  *
  *          No branch occurs; indicator 21 is off.
 C     FieldC        CABEQ     FieldD        TAGX                 21
 C                   :
 C     TAGX          TAG
```

Figure 273. CABxx Operations
