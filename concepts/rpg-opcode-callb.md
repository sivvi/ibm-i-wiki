---
title: CALLB (Call a Bound Procedure)
section: 780
---

# CALLB (Call a Bound Procedure)

Free-Form Syntax: (not allowed - use the CALLP operation code)

## Code

| Factor 1 | Factor 2 | Result Field | Indicators |
|----------|----------|--------------|------------|
| CALLB (D E) | Procedure name or procedure pointer | Plist name | _ | ER | LR |

The CALLB operation is used to call bound procedures written in any of the ILE languages.

The operation extender D may be used to include operational descriptors. This is similar to calling a prototyped procedure with CALLP when its parameters have been defined with keyword OPDESC. (Operational descriptors provide the programmer with run-time resolution of the exact attributes of character or graphic strings passed (that is, length and type of string). For more information, see chapter on calling programs and procedures in the Rational Development Studio for i: ILE RPG Programmer's Guide.

Factor 2 is required and must be a literal or constant containing the name of the procedure to be called, or a procedure pointer containing the address of the procedure to be called. All references must be able to be resolved at bind time. The procedure name provided is case sensitive and may contain more than 10 characters, but no more than 255. If the name is longer than 255, it will be truncated to 255. The result field is optional and may contain a PLIST name.

To handle CALLB exceptions (program status codes 202, 211, or 231), either the operation code extender 'E' or an error indicator ER can be specified, but not both. For more information on error handling, see "Program Exception/Errors" on page 179.

An indicator specified in positions 75-76 will be set on when the call ends with LR set on.

Note: The LR indicator is not allowed in a thread-safe environment.

For more information on call operations, see "Call Operations" on page 603.

## Example

```
 DName+++++++++++ETDsFrom+++To/L+++IDc.Keywords+++++++++++++++++++++++++++++
  * Define a procedure pointer
 D
 D ProcPtr         S               *   PROCPTR INZ(%PADDR('Create_Space'))
 D Extern          S             10
 CL0N01Factor1+++++++Opcode(E)+Factor2+++++++Result++++++++Len++D+HiLoEq....
  * The following call linkage would be STATIC
 C                   CALLB     'BOUNDPROC'
  * The following call linkage would be DYNAMIC
 C                   CALL      Extern
  * The following call linkage would be STATIC, using a procedure pointer
 C                   CALLB     ProcPtr
```

Figure 275. CALLB Operation
